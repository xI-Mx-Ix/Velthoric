plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    runs {
        server {
            runDir "run-server"
        }
        client {
            runDir "run-client"
            programArgs '--username', 'dev'
            //vmArg "-Dfabric.log.level=debug"
        }
    }

    accessWidenerPath = project(":common").loom.accessWidenerPath
}


configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:$mixinextras_version")))

    modImplementation "curse.maven:lithium-360438:6097158"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "curse.maven:sodium-394468:6260639"
    modImplementation "curse.maven:modmenu-308702:5162837"

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    implementation project(path: ":vx-native", configuration: "namedElements")
    minecraftRuntimeLibraries project(path: ":vx-native", configuration: "namedElements")
    shadowBundle project(path: ":vx-native", configuration: "namedElements")

    implementation 'com.github.luben:zstd-jni:1.5.7-6'
    minecraftRuntimeLibraries 'com.github.luben:zstd-jni:1.5.7-6'
    shadowBundle 'com.github.luben:zstd-jni:1.5.7-6'

    shadowBundle("com.github.stephengold:jolt-jni-Linux64:${jolt_jni_version}")

    common(project(path: ":vx-events", configuration: "namedElements")) { transitive false }
    shadowBundle project(path: ":vx-events", configuration: "transformProductionFabric")

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    def propertiesToExpand = [
            minecraft_version: rootProject.minecraft_version,
            mod_id: rootProject.mod_id,
            mod_name: rootProject.mod_name,
            mod_license: rootProject.mod_license,
            mod_version: rootProject.mod_version,
            mod_authors: rootProject.mod_authors,
            mod_description: rootProject.mod_description,
            minecraft_version_range: rootProject.minecraft_version_range,
            forge_version: rootProject.forge_version,
            forge_version_range: rootProject.forge_version_range,
            loader_version_range: rootProject.loader_version_range,

            fabric_loader_version: rootProject.fabric_loader_version,
            fabric_api_version: rootProject.fabric_api_version,
            architectury_api_version: rootProject.architectury_api_version
    ]

    inputs.properties propertiesToExpand

    filesMatching(['fabric.mod.json', 'pack.mcmeta']) {
        expand propertiesToExpand + [project: project]
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'

    // Natives are bundled in vx-native, no need to include them here
    exclude 'aix/**'
    exclude 'darwin/**'
    exclude 'freebsd/**'
    exclude 'linux/**'
    exclude 'win/**'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

// Configures the sourcesJar task to include source code from the common module
// ensuring that the resulting sources jar contains the full codebase.
tasks.named("sourcesJar") {
    def commonSources = project(":common").sourceSets.main.allSource
    from commonSources
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = base.archivesName.get()
            from components.java

            // Filters out development and shadow artifacts to reduce file size
            // and ensure only the production jar is published.
            artifacts.removeAll { artifact ->
                artifact.classifier in ['dev', 'shadow', 'dev-shadow']
            }
        }
    }
}

/**
 * Configure Velthoric Publishing for Fabric.
 */
velthoricPublishing {
    loader = "fabric"
    displayName = "Fabric"
    artifact = tasks.named("remapJar")
    dryRun = false
}