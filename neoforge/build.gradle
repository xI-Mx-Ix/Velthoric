plugins {
    id 'com.gradleup.shadow'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

loom {
    runs {
        server {
            runDir "run-server"
        }
        client {
            runDir "run-client"
            programArgs '--username', 'dev'
        }
    }

    accessWidenerPath = project(":common").loom.accessWidenerPath
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    mavenCentral()
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    implementation project(path: ":vx-native", configuration: "namedElements")
    minecraftRuntimeLibraries project(path: ":vx-native", configuration: "namedElements")
    shadowBundle project(path: ":vx-native", configuration: "namedElements")

    implementation 'com.github.luben:zstd-jni:1.5.7-6'
    minecraftRuntimeLibraries 'com.github.luben:zstd-jni:1.5.7-6'
    shadowBundle 'com.github.luben:zstd-jni:1.5.7-6'

    shadowBundle("com.github.stephengold:jolt-jni-Linux64:${jolt_jni_version}")


    common(project(path: ":vx-events", configuration: "namedElements")) { transitive false }
    shadowBundle project(path: ":vx-events", configuration: "transformProductionNeoForge")

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    var replaceProperties = [
            minecraft_version: rootProject.minecraft_version,
            minecraft_version_range: rootProject.minecraft_version_range,
            neoforge_version: rootProject.neoforge_version,
            neoforge_version_range: rootProject.neoforge_version_range,
            loader_version_range: rootProject.loader_version_range,
            mod_id: rootProject.mod_id,
            mod_name: rootProject.mod_name,
            mod_license: rootProject.mod_license,
            mod_version: rootProject.mod_version,
            mod_authors: rootProject.mod_authors,
            mod_description: rootProject.mod_description,
            architectury_api_version: rootProject.architectury_api_version
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'

    // Natives are bundled in vx-native, no need to include them here
    exclude 'aix/**'
    exclude 'darwin/**'
    exclude 'freebsd/**'
    exclude 'linux/**'
    exclude 'win/**'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

// Configures the sourcesJar task to include source code from the common module
// ensuring that the resulting sources jar contains the full codebase.
tasks.named("sourcesJar") {
    def commonSources = project(":common").sourceSets.main.allSource
    from commonSources
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = base.archivesName.get()
            from components.java

            // Filters out development and shadow artifacts to reduce file size
            // and ensure only the production jar is published.
            artifacts.removeAll { artifact ->
                artifact.classifier in ['dev', 'shadow', 'dev-shadow']
            }
        }
    }
}

/**
 * Configures the publication of the mod to external platforms.
 * Uses the remapped Jar file (production ready) as the artifact.
 */
publishMods {
    // --- Global Configuration ---
    file = tasks.named("remapJar").flatMap { it.archiveFile }
    changelog = rootProject.resolveChangelog()
    version = rootProject.mod_version
    displayName = "Velthoric ${rootProject.mod_version} for NeoForge ${rootProject.minecraft_version}"

    // --- CurseForge ---
    curseforge {
        accessToken = rootProject.curseforge_token
        projectId = "1367260"

        // We explicitly set the type to STABLE for CurseForge.
        // Beta or Alpha releases often have reduced visibility in the default
        // file lists or require users to change filter settings to be seen.
        type = STABLE

        minecraftVersions.add(rootProject.minecraft_version)

        modLoaders.add("neoforge")

        // Dependencies
        requires("architectury-api")
    }

    // --- Modrinth ---
    modrinth {
        accessToken = rootProject.modrinth_token
        projectId = "velthoric"

        // On Modrinth, we use the actual development state (BETA).
        // Unlike CurseForge, Modrinth does not restrict visibility for beta builds,
        // so we can accurately label the file without losing downloads.
        type = BETA

        minecraftVersions.add(rootProject.minecraft_version)

        modLoaders.add("neoforge")

        // Dependencies
        requires("architectury-api")
    }
}