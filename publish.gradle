/**
 * Global publishing configuration script.
 * Configures GitHub Release (Root) and Mod Publishing (Subprojects).
 *
 * Target Repository: https://github.com/xI-Mx-Ix/Velthoric
 */

// --- GitHub Release (Root Only) ---
if (project == rootProject) {
    apply plugin: "com.github.breadmoirai.github-release"

    githubRelease {
        token = rootProject.ext.has('github_token') ? rootProject.ext.github_token : ""
        owner = "xI-Mx-Ix"
        repo = "Velthoric"
        tagName = "${rootProject.mod_version}"
        releaseName = "${rootProject.mod_version}"
        targetCommitish = "1.20.1"
        body = rootProject.resolveChangelog()
        draft = false
        prerelease = false
        overwrite = true
    }
}

// --- Mod Loader Publishing Logic (Exposed to Subprojects) ---
ext.setupModPublishing = { String loaderName, String loaderDisplayName, TaskProvider<Jar> artifactTaskProvider ->
    if (!project.plugins.hasPlugin("me.modmuss50.mod-publish-plugin")) {
        apply plugin: "me.modmuss50.mod-publish-plugin"
    }

    publishMods {
        file = artifactTaskProvider.flatMap { it.archiveFile }
        changelog = rootProject.resolveChangelog()
        version = rootProject.mod_version
        displayName = "Velthoric ${rootProject.mod_version} for ${loaderDisplayName} ${rootProject.minecraft_version}"

        // --- CurseForge ---
        curseforge {
            accessToken = rootProject.ext.has('curseforge_token') ? rootProject.ext.curseforge_token : ""
            projectId = "1367260"

            // We explicitly set the type to STABLE for CurseForge.
            // Beta or Alpha releases often have reduced visibility in the default
            // file lists or require users to change filter settings to be seen.
            type = STABLE

            minecraftVersions.add(rootProject.minecraft_version)

            if (loaderName.equalsIgnoreCase("fabric")) {
                modLoaders.add("fabric")
                modLoaders.add("quilt")
                requires("architectury-api")
                requires("fabric-api")
            } else {
                modLoaders.add("forge")
                requires("architectury-api")
            }
        }

        // --- Modrinth ---
        modrinth {
            accessToken = rootProject.ext.has('modrinth_token') ? rootProject.ext.modrinth_token : ""
            projectId = "velthoric"

            // On Modrinth, we use the actual development state (BETA).
            // Unlike CurseForge, Modrinth does not restrict visibility for beta builds,
            // so we can accurately label the file without losing downloads.
            type = BETA

            minecraftVersions.add(rootProject.minecraft_version)

            if (loaderName.equalsIgnoreCase("fabric")) {
                modLoaders.add("fabric")
                modLoaders.add("quilt")
                requires("architectury-api")
                requires("fabric-api")
            } else {
                modLoaders.add("forge")
                requires("architectury-api")
            }
        }
    }
}